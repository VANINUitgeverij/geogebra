buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$rootProject.androidPluginVersion"
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group 'com.himamis.retex'

publishing {
    publications {
        library(MavenPublication) {
            artifact "${project.buildDir}/outputs/aar/${project.name}-${project.version}.aar"

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

dependencies {
	compile project(':renderer-base')
	testCompile 'junit:junit:4.11'
}

android {
	compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}
